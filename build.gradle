plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.61"
    id "application"
    id "com.github.gmazzo.buildconfig" version "1.7.1"
}

apply plugin: "kotlin-kapt"

sourceSets {
    main.kotlin.srcDirs += "src/main/kotlin"
    main.java.srcDirs += "src/main/java"
}
repositories {
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/arrow-kt/arrow-kt/" }
}

buildConfig {
    className("BuildInfo")
    packageName("badgerskt")
    buildConfigField("String", "APP_NAME", "\"${project.name}\"")
    buildConfigField("long", "BUILD_TIME", "${System.currentTimeMillis()}L")
    buildConfigField("String", "VERSION", "\"${System.getenv("BUILD_NUMBER") ?: "DEV-SNAPSHOT"}\"")
    buildConfigField("String", "GIT_COMMIT_HASH", "\"${System.getenv("GIT_COMMIT_HASH") ?: "n/a"}\"")
}

def arrowVersion = "0.10.4"
def ktor_version = '1.3.1'
def moshiVersion = "1.8.0"
def dropWizardMetricsVersion = "4.1.4"

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "com.typesafe:config:1.4.0"
    implementation "io.arrow-kt:arrow-core:$arrowVersion"
    implementation "io.arrow-kt:arrow-syntax:$arrowVersion"
    implementation "com.squareup.moshi:moshi:$moshiVersion"
    implementation "io.ktor:ktor-server-core:$ktor_version"
    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "io.ktor:ktor-locations:$ktor_version"
    implementation "ch.qos.logback:logback-classic:1.2.3"
    implementation "io.dropwizard.metrics:metrics-core:$dropWizardMetricsVersion"
    implementation "io.dropwizard.metrics:metrics-healthchecks:$dropWizardMetricsVersion"
    implementation "io.dropwizard.metrics:metrics-json:$dropWizardMetricsVersion"
    implementation "io.dropwizard.metrics:metrics-jvm:$dropWizardMetricsVersion"
    kapt "io.arrow-kt:arrow-meta:$arrowVersion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

application {
    mainClassName = 'badgerskt.BadgersAppKt'
}